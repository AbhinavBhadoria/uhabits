buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.2-5"
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'idea'
apply plugin: 'kotlin2js'
apply plugin: 'java'

group 'org.isoron.uhabits'
version '1.8.0'

jar.enabled = false

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:1.1.2-5"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.3"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/build/app/index.js"
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
    kotlinOptions.metaInfo = true
}

sourceSets {
    main.kotlin.srcDirs += "../electron-base/src/main/kotlin"
}

build.doLast() {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/build/app/node_modules"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
}

task copyResources(type: Copy) {
    includeEmptyDirs = false
    from "${projectDir}/src/main/resources"
    into "${projectDir}/build/app"
}

task compileSass(type: Exec) {
    commandLine "${projectDir}/tools/compile_sass.sh"
    workingDir "${projectDir}"
}

node {
    nodeModulesDir = file("${projectDir}/build/app")
}

npm_install.dependsOn(copyResources)
compileSass.dependsOn(npm_install)
build.finalizedBy(compileSass)
